buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'Kihira Repo'
        url = 'http://maven.kihirakreations.co.uk'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

project.ext {
    massive = "0"
    major = "1"
    minor = "1"
    mcVersion = "1.10.2"
    forgeVersion = "12.18.3.2185"
    buildVersion = System.getenv("BUILD_NUMBER")
}

project.version = "${project.ext.mcVersion}-${project.ext.massive}.${project.ext.major}.${project.minor}"
if (project.hasProperty("teamcity") && project.ext.buildVersion != null && teamcity["maven_release"] == null) {
    version = version + ".${System.getenv("BUILD_NUMBER")}"
}

group= "uk.kihira"
archivesBaseName = "BeProductive"

minecraft {
    version = "${project.ext.mcVersion}-${project.ext.forgeVersion}"
	mappings = "snapshot_20161111"
	runDir = "run"
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.ext.mcVersion
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.api.output
    classifier = 'dev'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

uploadArchives {
    dependsOn 'reobf'

    if (project.hasProperty("teamcity")) {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: teamcity["maven_url"]) {
                authentication(userName: teamcity["maven_username"], password: teamcity["maven_password"])
            }
        }
    }
}